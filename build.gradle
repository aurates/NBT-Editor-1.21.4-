plugins {
	id 'fabric-loom' version '1.15.4'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/nbteditor.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		exclude group: "net.fabricmc.fabric-api", module: "fabric-api-deprecated"
	}

	implementation files("nbteditor_1.17/build/devlibs/nbteditor_1.17-0.0.0-dev.jar")

	modImplementation "maven.modrinth:modmenu:11.0.3"
	modImplementation "maven.modrinth:nbt-autocomplete:1.3.10-fabric-1.21.1"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

compileJava.dependsOn ":nbteditor_1.17:jar"
remapSourcesJar.dependsOn ":nbteditor_1.17:jar"

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

def checkRefmap(Map json) {
	if (!(json.keySet() - ["mappings", "data"]).isEmpty()) return false
	if (!(json.data.keySet() - ["named:intermediary"]).isEmpty()) return false
	return true
}

task mergeRefmapJson {
	dependsOn classes
	dependsOn ":nbteditor_1.17:classes"

	doLast {
		File mainRefmap = file("build/classes/java/main/nbteditor-refmap.json")
		File additionalRefmap = file("nbteditor_1.17/build/classes/java/main/nbteditor_1.17-refmap.json")

		if (!mainRefmap.exists()) throw new GradleException("Missing main refmap!")
		if (!additionalRefmap.exists()) throw new GradleException("Missing 1.17 refmap!")

		def parser = new JsonSlurper()
		def mainJson = parser.parse(mainRefmap)
		def additionalJson = parser.parse(additionalRefmap)

		if (!checkRefmap(mainJson)) throw new GradleException("Main refmap is invalid!")
		if (!checkRefmap(additionalJson)) throw new GradleException("1.17 refmap is invalid!")

		mainJson.mappings += additionalJson.mappings
		mainJson.data["named:intermediary"] += additionalJson.data["named:intermediary"]

		mainRefmap.text = JsonOutput.prettyPrint(JsonOutput.toJson(mainJson))
	}
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}" }
	}

	dependsOn mergeRefmapJson
}

def getGitHash() {
	return "git rev-parse --verify --short HEAD".execute().text.trim()
}

task mergeDevLibs(type: Jar) {
	dependsOn remapJar
	dependsOn ":nbteditor_1.17:jar"

	from(zipTree(jar.archiveFile.get()))

	from(zipTree("nbteditor_1.17/build/devlibs/nbteditor_1.17-0.0.0-dev.jar")) {
		include "**/*.class"
	}

	archiveFile.set(file("${buildDir}/devlibs/nbteditor-${project.mod_version}-${getGitHash()}-dev.jar"))
}

task mergeLibs(type: Jar) {
	dependsOn remapJar
	dependsOn ":nbteditor_1.17:remapJar"

	from(zipTree(remapJar.archiveFile.get()))

	from(zipTree("nbteditor_1.17/build/libs/nbteditor_1.17-0.0.0.jar")) {
		include "**/*.class"
	}

	archiveFile.set(file("${buildDir}/libs/nbteditor-${project.mod_version}-${getGitHash()}.jar"))
}

build.dependsOn mergeDevLibs, mergeLibs

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}
	repositories {
	}
}

test {
	exclude '**/*'
}
